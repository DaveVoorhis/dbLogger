plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

group = 'org.reldb'
version = '1.0.1-SNAPSHOT'
description = 'A simple library for logging to SQL databases.'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.36.0.2'
}

clean {
    delete 'testdb.sqlite'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * This build.gradle will publish to the oss Nexus repository manager
 * at https://s01.oss.sonatype.org for staging to Maven Central.
 *
 * For that to work, you need a .gradle/gradle.properties file
 * with appropriate values for:
 *
 *   mavenCentralUsername= ...
 *   mavenCentralPassword= ...
 *   signing.keyId= ...
 *   signing.password= ...
 *   signing.secretKeyRingFile= ...
 *
 * The mavenCentral* values are used by publishing.
 * The signing.* values are used by signing.
 */

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    sign publishing.publications
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "dbLogger"
                description = "A simple library for logging to SQL databases."
                url = "https://github.com/DaveVoorhis/dbLogger"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "DaveVoorhis"
                        name = "Dave Voorhis"
                        email = "voorhis@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/DaveVoorhis/dbLogger.git"
                    developerConnection = "scm:git:https://github.com/DaveVoorhis/dbLogger.git"
                    url = "https://github.com/DaveVoorhis/dbLogger"
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = mavenCentralUsername
                password = mavenCentralPassword
            }
        }
    }
}
